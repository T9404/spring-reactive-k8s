import org.gradle.api.tasks.testing.logging.TestExceptionFormat

subprojects {
    var noSourceProjects = ['grpc']

    if (noSourceProjects.contains(project.name)) {
        return
    }

    apply plugin: 'java-library'

    dependencies {
        implementation libs.jetbrains.annotations
        implementation libs.spring.webflux

        compileOnly libs.lombok
        annotationProcessor libs.lombok

        testImplementation libs.mockito
        testImplementation libs.jupiter

//        compileOnly libs.spring.context.indexer
//        annotationProcessor libs.spring.context.indexer
    }

    test {
        jvmArgs([
                '--enable-preview',
                '--add-opens', 'jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED',
                '--add-opens', 'jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED',
                '--add-opens', 'jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED',
                '--add-opens', 'jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED',
                '--add-opens', 'jdk.compiler/com.sun.tools.javac.main=ALL-UNNAMED',
                '--add-opens', 'jdk.compiler/com.sun.tools.javac.jvm=ALL-UNNAMED',
                '--add-opens', 'jdk.compiler/com.sun.tools.javac.processing=ALL-UNNAMED',
                '--add-opens', 'jdk.compiler/com.sun.tools.javac.comp=ALL-UNNAMED',
                '--add-opens', 'jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED'
        ])
        useJUnitPlatform()
        systemProperty "junit.jupiter.extensions.autodetection.enabled", "true"
        testLogging {
            showStandardStreams = true
            exceptionFormat TestExceptionFormat.FULL
            showExceptions true
            showCauses true
            showStackTraces true
        }
    }

    repositories {
        mavenCentral()
    }
}